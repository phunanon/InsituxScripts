
;Poly-fill until higher Insitux version
(function rand to (/ (rand-num (* to 1000)) 1000))

(function pos-fmt pos
  (str (round (0 pos)) ", " (round (2 pos))))

(function rand-near pos dist
  ;Needs higher Insitux version:
  ;(let angle (rand (* 2 (pi))))
  (let angle (/ 50 (rand 314)))
  (let xz [(cos angle) (sin angle)])
  ;(let point [(cos angle) 0 (sin angle)])
  ;Broken in Deadline:
  ;(let point (for * [dist] point))
  (let point [(* (0 xz) dist) 0 (* (1 xz) dist)])
  (map + point pos))

;(function frac->coords map-name xyz
;  (let dimensions {
;    "dl_shipment" [[639 424 -134] [-494 414 -551]]})
;  (let dimensions (dimensions map-name))
;  (let diffs (.. map - dimensions))
;  (map + (map * xyz diffs) (1 dimensions)))

(function sent sender channel msg
;Needs higher Insitux version
; (when (starts-with? msg "(")
;   (dl.util.fmessage (eval msg)))

  (let parts (split msg))
  (let cmd (0 parts))
  (let body (join (sect parts)))
  (let body (if (!= body "") body))

  (when (= cmd "team")
    ((str "$dl.players." sender ".team")
     body)
    (dl.util.fmessage (str sender " team changed to " body)))

  (when (= cmd "kill")
    ((str "dl.players." body ".kill"))
    (dl.util.fmessage (str "Killed " body)))

  (when (= cmd "teleport")
    (let is-to (= (1 parts) "to"))
    (let a (if is-to (2 parts) sender))
    (let b (if is-to sender (1 parts)))
    ((str "$dl.players." a ".position")
     (position (str "dl.players." b)))
    (dl.util.fmessage (str "Teleported " a " to " b)))

  (when (= cmd "brrr")
    (dl.util.fmessage (str "Airstrike target locked: " body))
    (wait 1)
    (dl.util.fmessage "Coming in hot in 5")
    (wait 1)
    (dl.util.fmessage "4")
    (wait 1)
    (dl.util.fmessage "3")
    (wait 1)
    (dl.util.fmessage "2...")
    (wait 2)
    (let pos (position (str "dl.players." body)))
    (let i 0)
    (while (< i 100)
      (let i (inc i))
      (wait .01)
      (let rands [(rand 10) 0 (rand 10)])
      (dl.util.explosion (map + [(- (* i 2) 100) 0 0] rands pos)))
    (dl.util.fmessage (str body " has been brrr'd.")))

  (when (= cmd "drone")
    (let player (or body sender))
    (let pos (position (str "dl.players." player)))
    (dl.util.fmessage "Position locked!")
    (dl.util.fmessage "Drone strike in 4")
    (wait 1)
    (dl.util.fmessage "3")
    (wait 1)
    (dl.util.fmessage "2...")
    (wait 2)
    ;Replace with `range` once fixed in Deadline
    (let i 0)
    (let lim (+ 5 (rand 5)))
    (while (< i lim)
      (let i (inc i))
      (wait .25)
      (let rands [(rand 100) 30 (rand 100)])
      (dl.util.explosion (map + rands pos)))
    (dl.util.fmessage "Drone strike complete."))

  (when (= cmd "flare")
    (let who (or body sender))
    (let pos (position (str "dl.players." who)))
    (dl.util.explosion (map + [0 100 0] pos) "TestGrenade")
    (dl.util.fmessage (str "Fired flare above " who)))

  (when (= cmd "position")
    (let player (or body sender))
    (let pos (position (str "dl.players." player)))
    (dl.util.fmessage (pos-fmt pos)))

  (when (= cmd "heal")
    (let player (or body sender))
    ((str "$dl.players." player ".health") 100)
    (dl.util.fmessage (str "Healed " player)))

  (when (= cmd "explode")
    (let is-near (= (1 parts) "near"))
    (let player ((if is-near 2 1) parts))
    (let pos (position (str "dl.players." player)))
    (let pos (if is-near (rand-near pos 48) pos))
    (dl.util.explosion pos "TestGrenade")
    (dl.util.fmessage (str "Exploded " body)))

  (chatbot-extra sender channel msg))

(function chatbot-extra sender channel msg null)
(dl.events.on_chat_message.kill)
(dl.events.on_chat_message.connect sent)
(dl.util.fmessage "Loaded Insitux chatbot!
Note: where it says [player] replace with a player's name
    e.g. kill Player123
Commands:
  team security - switch to the Security team
  team insurgent - switch to the Insurgent team
  kill [player] - kill player
  heal - heal yourself fully
  heal [player] - heal a player fully
  drone - order drone strike on your position
  drone [player] - order drone strike on player's position
  brrr [player] - order airstrike on player
  flare - fire a flare above yourself
  flare [player] - fire a flare above a player
  explode [player] - kill player with an explosion
  explode near [player] - cause an explosion near the player
  position - write your position to the chat
  position [player] - write a player's position to the chat")
